
#Область API

&НаСервере
Процедура КомандаПолучитьQRКод()
	
	Обработка		= ОбработкаОбъект();
	Ответ			= Обработка.ПолучитьQRКод();
	Консоль			= СтруктураВJSONСтроку(Ответ);
	СтатусQRКода	= Ответ.type;

	Если Ответ.type = "qrCode" Тогда
		Элементы.QRКод.Видимость = Истина;
		Элементы.ГруппаQRКодГлавный .Видимость = Истина;
		QRКод = Обработка.ПолучитьМакет("QRМакет").ТекущаяОбласть.Текст;
		QRКод = СтрЗаменить(QRКод, "%QR_DATA%", Ответ.message);
	Иначе
		Элементы.QRКод.Видимость = Ложь;
		Элементы.ГруппаQRКодГлавный.Видимость = Ложь;
		QRКод = "";
	КонецЕсли;
	
КонецПроцедуры

#Область Аккаунт

&НаСервере
Процедура КомандаНачатьАвторизацию()
	Консоль = СтруктураВJSONСтроку(ОбработкаОбъект().НачатьАвторизацию(Параметр_phoneNumber));
КонецПроцедуры

&НаСервере
Процедура КомандаОтправитьКодАвторизации()
	Ответ = ОбработкаОбъект().ОтправитьКодАвторизации(Параметр_code, Параметр_password);
	Консоль = СтруктураВJSONСтроку(Ответ);
КонецПроцедуры

&НаСервере
Процедура КомандаОтправитьПарольАвторизации()
	Ответ = ОбработкаОбъект().ОтправитьПарольАвторизации(Параметр_password);
	Консоль = СтруктураВJSONСтроку(Ответ);
КонецПроцедуры

&НаСервере
Процедура КомандаРазлогинитьАккаунт()
	Консоль = СтруктураВJSONСтроку(ОбработкаОбъект().РазлогинитьАккаунт());
КонецПроцедуры

&НаСервере
Процедура КомандаПолучитьСостояниеАккаунта()
	Консоль = СтруктураВJSONСтроку(ОбработкаОбъект().ПолучитьСостояниеАккаунта());
КонецПроцедуры

&НаСервере
Процедура КомандаПерезапуститьАккаунт()
	Консоль = СтруктураВJSONСтроку(ОбработкаОбъект().ПерезапуститьАккаунт());
КонецПроцедуры

&НаСервере
Процедура КомандаПолучитьНастройкиАккаунта()
	Консоль = СтруктураВJSONСтроку(ОбработкаОбъект().ПолучитьНастройкиАккаунта());
КонецПроцедуры

&НаСервере
Процедура КомандаУстановитьНастройкиАккаунта(СтруктураНастроек)
	Консоль = СтруктураВJSONСтроку(ОбработкаОбъект().УстановитьНастройкиАккаунта(СтруктураНастроек));
КонецПроцедуры

&НаСервере
Процедура УстановитьФотоПрофиля(ПутьКФайлуНаСервере)
	Консоль = СтруктураВJSONСтроку(ОбработкаОбъект().УстановитьФотоПрофиля(ПутьКФайлуНаСервере));
КонецПроцедуры

&НаСервере
Процедура КомандаПолучитьИнформациюАккаунта()  	
	Консоль = СтруктураВJSONСтроку(ОбработкаОбъект().ПолучитьИнформациюОбАккаунте());	 
КонецПроцедуры

#КонецОбласти

#Область Отправка

&НаСервере
Процедура ОтправитьСообщениеВТелеграм()
	
	Ответ = ОбработкаОбъект().ОтправитьТекст(Параметр_chatId, Параметр_message);
	КонсольОтправка = СтруктураВJSONСтроку(Ответ);
	
	Сообщить("Сообщение отправлено успешно. idMessage=" + Ответ.idMessage);
	
КонецПроцедуры

&НаСервере
Процедура ВыбратьФотоДляАватараСервер(АдресХранилища, ИмяФайла)
	
	ИмяВременногоФайла = КаталогВременныхФайлов() + ИмяФайла;
	
	ДвоичныеДанные = ПолучитьИзВременногоХранилища(АдресХранилища);
	ДвоичныеДанные.Записать(ИмяВременногоФайла);
	
	Ответ = ОбработкаОбъект().УстановитьФотоПрофиля(ИмяВременногоФайла);
	
	Сообщить("ru = 'Фото установлено. setProfilePicture='; en = 'Picture successfully set. setProfilePicture='" + Ответ.setProfilePicture);
	
КонецПроцедуры


&НаСервере
Процедура ВыбратьКонтакт(ДанныеКонтакта)
	
	Телефон = ?(ДанныеКонтакта.Свойство("Телефон"), ДанныеКонтакта.Телефон, "");
	Имя     = ?(ДанныеКонтакта.Свойство("Имя"),     ДанныеКонтакта.Имя,     "");
	Фамилия = ?(ДанныеКонтакта.Свойство("Фамилия"), ДанныеКонтакта.Фамилия, "");
	
	ТекОбъект = РеквизитФормыВЗначение("Объект");
	
	Ответ = ТекОбъект.ОтправитьКонтакт(Параметр_chatId, Телефон, Имя, Фамилия);
	
	Если Ответ.Свойство("idMessage") Тогда
		Текст = НСтр("ru = 'Контакт отправлен успешно. idMessage='; en = 'Contact successfully sent. idMessage='") + Ответ.idMessage;
		Сообщить(Текст);
	Иначе
		Сообщить("Ошибка отправки (см. ответ сервера)");
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ВыбратьГеолокацию(Широта, Долгота)
	
	Ответ = ОбработкаОбъект().ОтправитьГеолокацию(Параметр_chatId, Широта, Долгота);
	КонсольОтправка = СтруктураВJSONСтроку(Ответ);
	
	Сообщить("ru = 'Геолокация отправлена успешно. idMessage='; en = 'Location successfully sent. idMessage='" + Ответ.idMessage);
	
КонецПроцедуры

&НаСервере
Процедура ОтправитьОпрос()
	Варианты = СтрРазделить(Параметр_options, ";", Ложь); 
	Ответ = ОбработкаОбъект().ОтправитьОпрос(Параметр_chatId, Параметр_message, Варианты);
	КонсольОтправка = СтруктураВJSONСтроку(Ответ);
КонецПроцедуры

#КонецОбласти

#Область Журнал

&НаСервере
Процедура КомандаПолучитьЖурналВходящихСообщений()
	Если ЗначениеЗаполнено(Интервал) Тогда
		Консоль = СтруктураВJSONСтроку(ОбработкаОбъект().ПолучитьЖурналВходящихСообщений(Интервал));
	Иначе
		Консоль = СтруктураВJSONСтроку(ОбработкаОбъект().ПолучитьЖурналВходящихСообщений());
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура КомандаПолучитьЖурналОтправленныхСообщений()
	Если ЗначениеЗаполнено(Интервал) Тогда
		Консоль = СтруктураВJSONСтроку(ОбработкаОбъект().ПолучитьЖурналОтправленныхСообщений(Интервал));
	Иначе
		Консоль = СтруктураВJSONСтроку(ОбработкаОбъект().ПолучитьЖурналОтправленныхСообщений());
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура КомандаПолучитьИсториюСообщенийЧата()
	Консоль = СтруктураВJSONСтроку(ОбработкаОбъект().ПолучитьИсториюСообщенийЧата(Параметр_chatId, КоличествоСообщений));
КонецПроцедуры

&НаСервере
Процедура КомандаПолучитьСообщениеЧата()
	Консоль = СтруктураВJSONСтроку(ОбработкаОбъект().ПолучитьСообщениеЧата(Параметр_chatId, Параметр_idMessage));
КонецПроцедуры

#КонецОбласти

#Область Очереди

&НаСервере
Процедура КомандаПолучитьКоличествоСообщенийКОтправке()
	КонсольОчередь = СтруктураВJSONСтроку(ОбработкаОбъект().ПолучитьКоличествоСообщенийКОтправке());
КонецПроцедуры

&НаСервере
Процедура КомандаПолучитьОчередьСообщенийКОтправке()
	КонсольОчередь = СтруктураВJSONСтроку(ОбработкаОбъект().ПолучитьОчередьСообщенийКОтправке());
КонецПроцедуры

&НаСервере
Процедура КомандаОчиститьОчередьСообщенийКОтправке()
	КонсольОчередь = СтруктураВJSONСтроку(ОбработкаОбъект().ОчиститьОчередьСообщенийКОтправке());
КонецПроцедуры

&НаСервере
Процедура КомандаПолучитьКоличествоУведомленийКОтправке()
	КонсольОчередь = СтруктураВJSONСтроку(ОбработкаОбъект().ПолучитьКоличествоУведомленийКОтправке());
КонецПроцедуры

&НаСервере
Процедура КомандаОчиститьОчередьВходящихУведомлений()
	КонсольОчередь = СтруктураВJSONСтроку(ОбработкаОбъект().ОчиститьОчередьВходящихУведомлений());
КонецПроцедуры

#КонецОбласти

#Область Группы

&НаСервере
Процедура КомандаСоздатьГруппу()	
	
	Ответ = ОбработкаОбъект().СоздатьГруппу(Параметр_groupName, Параметр_chatIds.ВыгрузитьЗначения());
	
	Если ЗначениеЗаполнено(Ответ) Тогда
		Консоль = СтруктураВJSONСтроку(Ответ);
		ИДгруппы = Ответ.chatId;
		Параметр_chatId = Ответ.chatId;
	КонецЕсли;	
	
КонецПроцедуры

&НаСервере
Процедура КомандаИзменитьИмяГруппы()
	
	Консоль = СтруктураВJSONСтроку(ОбработкаОбъект().ИзменитьИмяГруппы(ИДгруппы, Параметр_groupName));
	
КонецПроцедуры

&НаСервере
Процедура КомандаПолучитьИнформациюОГруппе()
	
	Консоль = СтруктураВJSONСтроку(ОбработкаОбъект().ПолучитьИнформациюОГруппе(ИДгруппы));
	
КонецПроцедуры

&НаСервере
Процедура КомандаДобавитьУчастникаВГруппу()

	Консоль = СтруктураВJSONСтроку(ОбработкаОбъект().ДобавитьУчастникаВГруппу(ИДгруппы, Параметр_participantChatId));

КонецПроцедуры

&НаСервере
Процедура КомандаУдалитьУчастникаИзГруппы()

	Консоль = СтруктураВJSONСтроку(ОбработкаОбъект().УдалитьУчастникаИзГруппы(ИДгруппы, Параметр_participantChatId));

КонецПроцедуры

&НаСервере
Процедура КомандаНазначитьПраваАдминистратораГруппы()

	Консоль = СтруктураВJSONСтроку(ОбработкаОбъект().НазначитьПраваАдминистратораГруппы(ИДгруппы, Параметр_participantChatId));

КонецПроцедуры

&НаСервере
Процедура КомандаОтозватьПраваАдминистратораГруппы()

	Консоль = СтруктураВJSONСтроку(ОбработкаОбъект().ОтозватьПраваАдминистратораГруппы(ИДгруппы, Параметр_participantChatId));

КонецПроцедуры

&НаСервере
Процедура КомандаВыйтиИзГруппы()

	Консоль = СтруктураВJSONСтроку(ОбработкаОбъект().ВыйтиИзГруппы(ИДгруппы));

КонецПроцедуры

#Конецобласти

#Область ОтметкаПрочтения

Процедура КомандаОтметитьЧатПрочитанным()

	Консоль = СтруктураВJSONСтроку(ОбработкаОбъект().ОтметитьЧатПрочитанным(Параметр_chatId, Параметр_idMessage));

КонецПроцедуры

#КонецОбласти

#Область Партнер 

&НаСервере
Процедура ПолучитьВсеИнстансыТелеграм()                                                                                       
	
	Ответ          = ОбработкаОбъект().ПолучитьВсеИнстансы(); 	
	КонсольПартнер = СтруктураВJSONСтроку(Ответ);
	
КонецПроцедуры  

&НаСервере
Процедура СоздатьИнстансТелеграм()                                                                                       
	
	Ответ          = ОбработкаОбъект().СоздатьИнстанс(); 	
	КонсольПартнер = СтруктураВJSONСтроку(Ответ); 
	
	Ответ.Свойство("idInstance", Параметр_idInstance);	
	
КонецПроцедуры  

&НаСервере
Процедура УдалитьИнстансТелеграм()                                                                                       
	
	Ответ          = ОбработкаОбъект().УдалитьИнстанс(Параметр_idInstance); 	
	КонсольПартнер = СтруктураВJSONСтроку(Ответ);
	
КонецПроцедуры 

#КонецОбласти

#Область СервисныеМетоды

&НаСервере
Процедура КомандаПроверитьНаличиеТелеграм()
	ОтветСервера = ОбработкаОбъект().ПроверитьНаличиеТелеграм(Число(Параметр_phoneNumber));
    Консоль = СтруктураВJSONСтроку(ОтветСервера);
	
КонецПроцедуры

&НаСервере
Процедура КомандаПолучитьАватарКонтакта()

	Консоль = СтруктураВJSONСтроку(ОбработкаОбъект().ПолучитьАватарКонтакта(Параметр_chatId));
	
КонецПроцедуры

&НаСервере
Процедура КомандаПолучитьКонтакты()

	Консоль = СтруктураВJSONСтроку(ОбработкаОбъект().ПолучитьКонтакты());
	
КонецПроцедуры

&НаСервере
Процедура КомандаПолучитьИнфоКонтакта()
	
	Консоль = СтруктураВJSONСтроку(ОбработкаОбъект().ПолучитьИнфоКонтакта(Параметр_chatId));

КонецПроцедуры

&НаСервере
Процедура КомандаУдалитьСообщение()
	
	Консоль = СтруктураВJSONСтроку(ОбработкаОбъект().УдалитьСообщение(Параметр_chatId, Параметр_idMessage));
	
	Если Консоль = "true" Тогда
		Сообщить("Сообщение успешно удалено!");
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура КомандаИзменитьСообщение()
	
	Консоль = СтруктураВJSONСтроку(ОбработкаОбъект().ИзменитьСообщение(Параметр_chatId, Параметр_idMessage, Параметр_message));
	
	Если Консоль = "true" Тогда
		Сообщить("Сообщение успешно изменено!");
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура КомандаПолучитьЧаты()
	
	Консоль = СтруктураВJSONСтроку(ОбработкаОбъект().ПолучитьЧаты());

КонецПроцедуры

&НаСервере
Процедура КомандаОтправитьУведомлениеОНабореТекста()
	
	Консоль = СтруктураВJSONСтроку(ОбработкаОбъект().ОтправитьУведомлениеОНабореТекста(Параметр_chatId));

	Если Консоль = "true" Тогда
		Сообщить("Уведомление отправлено!");
	КонецЕсли;

КонецПроцедуры
    
#КонецОбласти

&НаСервере
Процедура КомандаСкачатьФайлИзВходящегоУведомления()
	
	Ответ = ОбработкаОбъект().СкачатьФайлИзВходящегоУведомления(Параметр_chatId, Параметр_idMessage);
	Получение_Тело = СтруктураВJSONСтроку(Ответ);
	Сообщить("Ссылка для скачивания downloadUrl=" + Ответ.downloadUrl);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытий

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ПодготовитьНастройки();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Элементы.ДеталиПредупреждениеСтандартныеНастройки.Видимость = Ложь;

	Если Не ЗначениеЗаполнено(Объект.host) Тогда
		ПараметрыХостовПоУмоланию(Истина, Ложь);
	КонецЕсли;

	Если Не ЗначениеЗаполнено(Объект.mediaHost) Тогда
		ПараметрыХостовПоУмоланию(Ложь, Истина);
	КонецЕсли;
	
	ОбновитьСтатусСервиса();
	ПереключательТипаЧатаПриИзменении();
	ПриИзмененииIDЧата();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)
	
	Если ЗначениеЗаполнено(ВыбранноеЗначение) И ПроверитьЗаполнение() Тогда
		Если ВыбранноеЗначение.ИмяФормы = "ВыборСсылкиНаФайл" Тогда
			ВыбратьСсылкуДляОтправкиСервер(ВыбранноеЗначение.АдресФайла, ВыбранноеЗначение.ИмяФайла);
		ИначеЕсли ВыбранноеЗначение.ИмяФормы = "ВыборКонтакта" Тогда
			ВыбратьКонтакт(ВыбранноеЗначение)
		ИначеЕсли ВыбранноеЗначение.ИмяФормы = "ВыборГеолокации" Тогда
			ВыбратьГеолокацию(ВыбранноеЗначение.Широта,ВыбранноеЗначение.Долгота)
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиРеквизитовКомандФормы

&НаКлиенте
Процедура ДекорацияПерейтиПоСсылкеВПодсказке(Элемент)
	
	ПерейтиПоНавигационнойСсылке(Элемент.Подсказка);
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияКакПолучитьChatIdНажатие(Элемент)
	
	ПерейтиПоНавигационнойСсылке("https://green-api.com/telegram/docs/api/chat-id/");
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияЗакрытьОшибкиНажатие(Элемент)
	
	СброситьОшибки();
	
КонецПроцедуры

&НаКлиенте
Процедура ПереключательТипаЧатаПриИзменении(Элемент = Неопределено)
	
	ПереключательТипаЧата = СтрНачинаетсяС(Параметр_chatId, "-");
	ЗаголовокЧата = ?(ПереключательТипаЧата, "ID группы", "ID чата");
	Если ПереключательТипаЧата Тогда
		ИДгруппы = Параметр_chatId;
	КонецЕсли;
	
	ЭлементыИдЧатаГруппы = СтрРазделить("ИдЧата,ИдЧатаЖурнала,ИдЧатаОтметка,СкачатьИдЧата,ИдЧатаСервисные", ",");
	Для Каждого Элемент Из ЭлементыИдЧатаГруппы Цикл
		Элементы.Найти(Элемент).Заголовок = ЗаголовокЧата;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьСтандартныеНастройкиПараметрыХостов(Команда)

	ПоказатьВопрос(
		Новый ОписаниеОповещения("ПослеЗакрытияВопросаСтандартныеНастройки", ЭтотОбъект),
		"Установить универсальные значения для ""host"" и ""mediaHost""?",
		РежимДиалогаВопрос.ДаНет);

КонецПроцедуры

&НаКлиенте
Процедура ПослеЗакрытияВопросаСтандартныеНастройки(Результат, Параметры) Экспорт

	Если Результат = КодВозвратаДиалога.Нет Тогда
		Возврат;
	КонецЕсли;

	ПараметрыХостовПоУмоланию();

КонецПроцедуры

&НаКлиенте
Процедура ДекорацияЗакрытьQRглавныйНажатие(Элемент)
	
	Элементы.ГруппаQRКодГлавный.Видимость = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьСтатус(Команда)
	
	ОбновитьСтатусСервиса();
    ПоказатьОповещениеПользователя("Обновлено",,"Получен статус");

КонецПроцедуры

&НаКлиенте
Процедура ОбновитьТипЧата()
	
	ПереключательТипаЧата = СтрНачинаетсяС(Параметр_chatId, "-");
	ПереключательТипаЧатаПриИзменении();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриИзмененииIDЧата(Элемент = Неопределено)
	
	ОбновитьТипЧата();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриИзмененииНастройкиAPI(Элемент)
	
	ПутьКДанным = ПолучитьПутьКДанным(Элемент.Имя);
	Настройка = ИзвлечИзИмениПутьКAPI(ПутьКДанным);
	
	Значение = ЭтотОбъект[ПутьКДанным];
	ЭтоБулево = ТипЗнч(Значение) = Тип("Булево");
	
	КомандаУстановитьНастройкиАккаунта(Новый Структура(Настройка.Имя, ?(ЭтоБулево,
																			Формат(Значение,"БЛ=no; БИ=yes"),
																			Значение)));
	
КонецПроцедуры

&НаКлиенте
Функция Телеграмироватьhost(строка)
	
	Возврат СтрЗаменить(СтрЗаменить(строка, "https://", ""), "http://", "");
	
КонецФункции
&НаКлиенте
Процедура apiUrlОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка)	
	Объект.host = Телеграмироватьhost(Текст);
КонецПроцедуры
&НаКлиенте
Процедура mediaUrl_ПодключениеОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка)
	Объект.mediaHost = Телеграмироватьhost(Текст);
КонецПроцедуры


#Область Аккаунт

&НаКлиенте
Процедура ПолучитьQRКод(Команда)
	КомандаПолучитьQRКод();
КонецПроцедуры

&НаКлиенте
Процедура НачатьАвторизацию(Команда)
	
	Если Не ЗначениеЗаполнено(Параметр_phoneNumber) Тогда	
		ПоказатьСообщениеПользователю("Поле ""Номер телефона"" не заполнено", "Параметр_phoneNumber");
		Возврат;
	КонецЕсли;	
	
	КомандаНачатьАвторизацию();
	
КонецПроцедуры
&НаКлиенте
Процедура ОтправитьКодАвторизации(Команда)
	
	Если Не ЗначениеЗаполнено(Параметр_code) Тогда	
		ПоказатьСообщениеПользователю("Поле ""код авторизации из смс"" не заполнено", "Параметр_code");
		Возврат;
	КонецЕсли;	
	
	КомандаОтправитьКодАвторизации();
	
КонецПроцедуры
&НаКлиенте
Процедура ОтправитьПарольАвторизации(Команда)
	
	Если Не ЗначениеЗаполнено(Параметр_password) Тогда	
		ПоказатьСообщениеПользователю("Поле 'Пароль' не заполнено", "Параметр_password");
		Возврат;
	КонецЕсли;	
	
	КомандаОтправитьПарольАвторизации();
	
КонецПроцедуры
&НаКлиенте
Процедура РазлогинитьИнстанс(Команда)
	КомандаРазлогинитьАккаунт();	
КонецПроцедуры
&НаКлиенте
Процедура ПолучитьСостояниеИнстанса(Команда)
	КомандаПолучитьСостояниеАккаунта();
КонецПроцедуры
&НаКлиенте
Процедура перезапуститьИнстанс(Команда)
	КомандаПерезапуститьАккаунт();
КонецПроцедуры
&НаКлиенте
Процедура ПолучитьНастройкиИнстанса(Команда)
	КомандаПолучитьНастройкиАккаунта();
КонецПроцедуры
&НаКлиенте
Процедура УстановкаАватараАккаунта(Команда)
	Диалог = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
	Оповещение = Новый ОписаниеОповещения("ВыбратьФотоДляАватара", ЭтотОбъект);
	Диалог.Показать(Оповещение);
КонецПроцедуры
&НаКлиенте
Процедура ПолучитьИнформациюОбАккаунте(Команда)
	КомандаПолучитьИнформациюАккаунта();
КонецПроцедуры

#КонецОбласти

#Область Отправка

&НаКлиенте
Процедура ОтправитьТекст(Команда)
	
	Если Не ЗначениеЗаполнено(Параметр_chatId) Тогда	
		ПоказатьСообщениеПользователю("Поле ""ID чата"" не заполнено", "Параметр_chatId");
		Возврат;
	КонецЕсли;
	
	ОтправитьСообщениеВТелеграм();  

КонецПроцедуры
&НаКлиенте
Процедура ОтправитьФайл(Команда)
		
	Диалог     = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
	Оповещение = Новый ОписаниеОповещения("ВыбратьФайлДляОтправки", ЭтотОбъект);

	Диалог.Показать(Оповещение);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьФайлПоСсылке(Команда)
	ОткрытьФормуОбработки("ВыборСсылкиНаФайл");
КонецПроцедуры
&НаКлиенте
Процедура ВыгрузитьФайл(Команда)
	
	Диалог = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
	ОтправитьСсылку = Ложь;
	Оповещение = Новый ОписаниеОповещения("ВыбратьФайлДляЗагрузки", ЭтотОбъект, ОтправитьСсылку);
	Диалог.Показать(Оповещение);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьГеолокацию(Команда)
	
	Если Не ЗначениеЗаполнено(Параметр_chatId) Тогда	
		ПоказатьСообщениеПользователю("Поле ""ID чата"" не заполнено", "Параметр_chatId");
		Возврат;
	КонецЕсли;
	ОткрытьФормуОбработки("ВыборГеолокации");
	
КонецПроцедуры
&НаКлиенте
Процедура ОтправитьКонтакт(Команда)

	Если Не ЗначениеЗаполнено(Параметр_chatId) Тогда	
		ПоказатьСообщениеПользователю("Поле ""ID чата"" не заполнено", "Параметр_chatId");
		Возврат;
	КонецЕсли;
	ОткрытьФормуОбработки("ВыборКонтакта");
	
КонецПроцедуры

#КонецОбласти

#Область Получение

&НаКлиенте
Процедура ПолучитьВсеСообщенияЧерезВебхук(Команда)
	
	РежимДиалога   = РежимДиалогаВопрос.ДаНет;
	Текстсообщения = "После выполнения команды очередь сообщений будет очищена. Продолжить?";
	Оповещение     = Новый ОписаниеОповещения("ПослеЗакрытияВопросаПолучитьВсеСообщенияЧерезВебхук", ЭтотОбъект);
	
	ПоказатьВопрос(Оповещение, Текстсообщения, РежимДиалога, 0);
	
КонецПроцедуры 
&НаКлиенте
Процедура ПослеЗакрытияВопросаПолучитьВсеСообщенияЧерезВебхук(Результат, Параметры) Экспорт
	
    Если Результат = КодВозвратаДиалога.Нет Тогда
        Возврат;
	КонецЕсли;
	
	СтатусПолученияВсехСообщений = "Ожидание получения всех сообщений...";
	НажатиеКнопкиОжидатьСообщения();

КонецПроцедуры
&НаКлиенте
Процедура ПолучитьОдноСообщенияЧерезВебхук(Команда)
	
	ПолученныеСообщения.Очистить();
		
	СтатусПолученияВсехСообщений = "Ожидаем одно сообщение ...";
	ОбновитьОтображениеДанных(Элементы.СтатусПолученияВсехСообщений);
		
	ПодключитьОбработчикОжидания("Подключаемый_ПолучитьВсеСообщенияЧерезВебхук", 1, Истина);
	Получение_РежимОжидания = Ложь;
	
КонецПроцедуры
&НаКлиенте
Процедура НажатиеКнопкиОжидатьСообщения()
	
	Если Получение_РежимОжидания Тогда
		
		ОтключитьОбработчикОжидания("Подключаемый_ПолучитьВсеСообщенияЧерезВебхук");
		
		СтатусПолученияВсехСообщений = "";
		Элементы.ПолучитьВсеСообщенияЧерезВебхук.Заголовок = "Ожидать сообщения";
		
	Иначе
		
		ПолученныеСообщения.Очистить();
		
		СтатусПолученияВсехСообщений = "Ожидаем новые сообщения ...";
		Элементы.ПолучитьВсеСообщенияЧерезВебхук.Заголовок = "Прекратить ожидать сообщения";
		ОбновитьОтображениеДанных(Элементы.СтатусПолученияВсехСообщений);
		
		ПодключитьОбработчикОжидания("Подключаемый_ПолучитьВсеСообщенияЧерезВебхук", 1, Ложь);
		
	КонецЕсли;
	Получение_РежимОжидания = Не Получение_РежимОжидания;

КонецПроцедуры
&НаКлиенте
Процедура Подключаемый_ПолучитьВсеСообщенияЧерезВебхук()
	
	Ответ = ПолучитьВсеСообщенияЧерезВебхукСтрокой();
	
	Если Не Получение_РежимОжидания Тогда
		СтатусПолученияВсехСообщений = "";
	КонецЕсли;
	
	Если ПустаяСтрока(Ответ) Тогда		
		ПоказатьОповещениеПользователя("Нет сообщений",,"Запрос не вернул результат");
		Получение_РежимОжидания = Истина;
		НажатиеКнопкиОжидатьСообщения();
		Возврат;
	КонецЕсли;
	
	Получение_Тело = Ответ + ?(ПустаяСтрока(Получение_Тело), "", "
		|
		|-----------------
		|
		|" + Получение_Тело);
	
	ОбновитьОтображениеДанных(Элементы.ТекстПолученноеСообщение);
	
КонецПроцедуры
&НаСервере
Функция ПолучитьВсеСообщенияЧерезВебхукСтрокой()
	
	ОбработкаОбъект = ОбработкаОбъект();
	Структура = ОбработкаОбъект.ПолучитьСообщение();
	Возврат ?(ЗначениеЗаполнено(Структура), СтруктураВJSONСтроку(Структура), "");
	
КонецФункции
&НаКлиенте
Процедура СкачатьФайлИзСообщения(Команда)
	Если Не ЗначениеЗаполнено(Параметр_chatId) Тогда	
		ПоказатьСообщениеПользователю("Поле ""ID чата"" не заполнено", "Параметр_chatId");
		Возврат;
	КонецЕсли;
	Если Не ЗначениеЗаполнено(Параметр_idMessage) Тогда	
		ПоказатьСообщениеПользователю("Поле ""ID сообщения"" не заполнено", "Параметр_idMessage");
		Возврат;
	КонецЕсли;
	КомандаСкачатьФайлИзВходящегоУведомления();
КонецПроцедуры

#КонецОбласти

#Область Журнал

&НаКлиенте
Процедура ПолучитьЖурналВходящихСообщений(Команда)
	КомандаПолучитьЖурналВходящихСообщений();
КонецПроцедуры
&НаКлиенте
Процедура ПолучитьЖурналОтправленныхСообщений(Команда)
	КомандаПолучитьЖурналОтправленныхСообщений();
КонецПроцедуры
&НаКлиенте
Процедура ПолучитьИсториюСообщенияЧата(Команда)
	Если Не ЗначениеЗаполнено(Параметр_chatId) Тогда	
		ПоказатьСообщениеПользователю("Поле ""ID чата"" не заполнено", "Параметр_chatId");
		Возврат;
	КонецЕсли;
	КомандаПолучитьИсториюСообщенийЧата();
КонецПроцедуры
&НаКлиенте
Процедура ПолучитьСообщениеЧата(Команда)
	Если Не ЗначениеЗаполнено(Параметр_chatId) Тогда	
		ПоказатьСообщениеПользователю("Поле ""ID чата"" не заполнено", "Параметр_chatId");
		Возврат;
	КонецЕсли;
	Если Не ЗначениеЗаполнено(Параметр_idMessage) Тогда	
		ПоказатьСообщениеПользователю("Поле ""ID сообщения"" не заполнено", "Параметр_idMessage");
		Возврат;
	КонецЕсли;
	КомандаПолучитьСообщениеЧата();
КонецПроцедуры
#КонецОбласти

#Область Очередь

&НаКлиенте
Процедура ПолучитьКоличествоСообщенийКОтправке(Команда)
	КомандаПолучитьКоличествоСообщенийКОтправке();
КонецПроцедуры

&НаКлиенте
Процедура ПолучитьОчередьСообщенийКОтправке(Команда)
	КомандаПолучитьОчередьСообщенийКОтправке();
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьОчередьСообщенийКОтправке(Команда)
	КомандаОчиститьОчередьСообщенийКОтправке();
КонецПроцедуры

&НаКлиенте
Процедура ПолучитьКоличествоУведомленийКОтправке(Команда)
	КомандаПолучитьКоличествоУведомленийКОтправке();
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьОчередьВходящихУведомлений(Команда)
	КомандаОчиститьОчередьВходящихУведомлений();
КонецПроцедуры

#КонецОбласти

#Область Группы

&НаКлиенте
Процедура СоздатьГруппу(Команда)
	
	Если Не ЗначениеЗаполнено(Параметр_groupName) Тогда	
		ПоказатьСообщениеПользователю("Поле ""Имя группы"" не заполнено", "Параметр_groupName");
		Возврат;
	КонецЕсли;
	Если Не Параметр_chatIds.Количество() Тогда	
		ПоказатьСообщениеПользователю("Не заполнены ID участников группы", "Параметр_chatIds");
		Возврат;
	КонецЕсли;
	КомандаСоздатьГруппу();
	Параметр_chatId = ИДгруппы;
	ОбновитьТипЧата();
	
КонецПроцедуры
&НаКлиенте
Процедура ИзменитьИмяГруппы(Команда)
	
	Если Не ЗначениеЗаполнено(ИДгруппы) Тогда	
		ПоказатьСообщениеПользователю("Поле ""ID группы"" не заполнено", "ИДгруппы");
		Возврат;
	КонецЕсли;
	Если Не ЗначениеЗаполнено(Параметр_groupName) Тогда	
		ПоказатьСообщениеПользователю("Поле ""Имя группы"" не заполнено", "Параметр_groupName");
		Возврат;
	КонецЕсли;
	
	КомандаИзменитьИмяГруппы();
	
КонецПроцедуры
&НаКлиенте
Процедура ПолучитьИнформациюОГруппе(Команда)
	
	Если Не ЗначениеЗаполнено(ИДгруппы) Тогда	
		ПоказатьСообщениеПользователю("Поле ""ID группы"" не заполнено", "ИДгруппы");
		Возврат;
	КонецЕсли;
	
	КомандаПолучитьИнформациюОГруппе();
	
КонецПроцедуры
&НаКлиенте
Процедура ДобавитьУчастникаВГруппу(Команда)
	
	Если Не ЗначениеЗаполнено(ИДгруппы) Тогда	
		ПоказатьСообщениеПользователю("Поле ""ID группы"" не заполнено", "ИДгруппы");
		Возврат;
	КонецЕсли;
	Если Не ЗначениеЗаполнено(Параметр_participantChatId) Тогда	
		ПоказатьСообщениеПользователю("Поле ""ID чата участника"" не заполнено", "Параметр_participantChatId");
		Возврат;
	КонецЕсли;
	
	КомандаДобавитьУчастникаВГруппу();
	
КонецПроцедуры
&НаКлиенте
Процедура УдалитьУчастникаИзГруппы(Команда)
	
	Если Не ЗначениеЗаполнено(ИДгруппы) Тогда	
		ПоказатьСообщениеПользователю("Поле ""ID группы"" не заполнено", "ИДгруппы");
		Возврат;
	КонецЕсли;
	Если Не ЗначениеЗаполнено(Параметр_participantChatId) Тогда	
		ПоказатьСообщениеПользователю("Поле ""ID чата участника"" не заполнено", "Параметр_participantChatId");
		Возврат;
	КонецЕсли;
	
	КомандаУдалитьУчастникаИзГруппы();
	
КонецПроцедуры
&НаКлиенте
Процедура НазначитьПраваАдминистратораГруппы(Команда)
	
	Если Не ЗначениеЗаполнено(ИДгруппы) Тогда	
		ПоказатьСообщениеПользователю("Поле ""ID группы"" не заполнено", "ИДгруппы");
		Возврат;
	КонецЕсли;
	Если Не ЗначениеЗаполнено(Параметр_participantChatId) Тогда	
		ПоказатьСообщениеПользователю("Поле ""ID чата участника"" не заполнено", "Параметр_participantChatId");
		Возврат;
	КонецЕсли;
	
	КомандаНазначитьПраваАдминистратораГруппы();
	
КонецПроцедуры
&НаКлиенте
Процедура ОтозватьПраваАдминистратораГруппы(Команда)
	
	Если Не ЗначениеЗаполнено(ИДгруппы) Тогда	
		ПоказатьСообщениеПользователю("Поле ""ID группы"" не заполнено", "ИДгруппы");
		Возврат;
	КонецЕсли;
	Если Не ЗначениеЗаполнено(Параметр_participantChatId) Тогда	
		ПоказатьСообщениеПользователю("Поле ""ID чата участника"" не заполнено", "Параметр_participantChatId");
		Возврат;
	КонецЕсли;
	
	КомандаОтозватьПраваАдминистратораГруппы();
	
КонецПроцедуры
&НаКлиенте
Процедура ВыйтиИзГруппы(Команда)

	Если Не ЗначениеЗаполнено(ИДгруппы) Тогда	
		ПоказатьСообщениеПользователю("Поле ""ID группы"" не заполнено", "ИДгруппы");
		Возврат;
	КонецЕсли;
	
	КомандаВыйтиИзГруппы();
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьАватарГруппы(Команда)
	
	Если Не ЗначениеЗаполнено(ИДгруппы) Тогда	
		ПоказатьСообщениеПользователю("Поле ""ID группы"" не заполнено", "ИДгруппы");
		Возврат;
	КонецЕсли;
	
	Диалог = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
	Оповещение = Новый ОписаниеОповещения("ВыбратьФотоДляАватараГруппы", ЭтотОбъект);
	Диалог.Показать(Оповещение);
	
КонецПроцедуры
&НаКлиенте
Процедура ВыбратьФотоДляАватараГруппы(ВыбранныеФайлы, ДопПараметры) Экспорт
	
	Если ВыбранныеФайлы = Неопределено Или ВыбранныеФайлы.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Файл = Новый Файл(ВыбранныеФайлы[0]);
	
	АдресХранилища = ПоместитьВоВременноеХранилище(Новый ДвоичныеДанные(Файл.ПолноеИмя));
	
	ВыбратьФотоДляАватараГруппыСервер(АдресХранилища, Файл.Имя);
	
КонецПроцедуры

&НаСервере
Процедура ВыбратьФотоДляАватараГруппыСервер(АдресХранилища, ИмяФайла)
	
	ИмяВременногоФайла = КаталогВременныхФайлов() + ИмяФайла;
	
	ДвоичныеДанные = ПолучитьИзВременногоХранилища(АдресХранилища);
	ДвоичныеДанные.Записать(ИмяВременногоФайла);
	
	Ответ = ОбработкаОбъект().УстановитьФотоГруппы(ИДгруппы, ИмяВременногоФайла);
	Консоль = СтруктураВJSONСтроку(Ответ);

	Сообщить("Фото группы установлено. setGroupPicture=" + Ответ.setGroupPicture);
	
КонецПроцедуры


#КонецОбласти

#Область ОтметкаПрочтения

&НаКлиенте
Процедура ОтметитьЧатПрочитанным(Команда)
	
	Если Не ЗначениеЗаполнено(Параметр_chatId) Тогда	
		ПоказатьСообщениеПользователю("Поле ""Имя чата"" не заполнено", "Параметр_chatId");
		Возврат;
	КонецЕсли;
	
	КомандаОтметитьЧатПрочитанным();
	
КонецПроцедуры

#КонецОбласти

#Область СервисныеМетоды

&НаКлиенте
Процедура ПроверитьНаличиеТелеграм(Команда)
	
	Если Не ЗначениеЗаполнено(Параметр_phoneNumber) Тогда	
		ПоказатьСообщениеПользователю("Поле ""номер телефона"" не заполнено", "Параметр_phoneNumber");
		Возврат;
	КонецЕсли;
	
	КомандаПроверитьНаличиеТелеграм();
	
КонецПроцедуры
&НаКлиенте
Процедура ПолучитьАватарКонтакта(Команда)
	
	Если Не ЗначениеЗаполнено(Параметр_chatId) Тогда	
		ПоказатьСообщениеПользователю("Поле ""ID чата"" не заполнено", "Параметр_chatId");
		Возврат;
	КонецЕсли;
	
	КомандаПолучитьАватарКонтакта();
	
КонецПроцедуры
&НаКлиенте
Процедура ПолучитьКонтакты(Команда)
	
	КомандаПолучитьКонтакты();
	
КонецПроцедуры
&НаКлиенте
Процедура ПолучитьИнфоКонтакта(Команда)
	
	Если Не ЗначениеЗаполнено(Параметр_chatId) Тогда	
		ПоказатьСообщениеПользователю("Поле ""ID чата"" не заполнено", "Параметр_chatId");
		Возврат;
	КонецЕсли;
	
	КомандаПолучитьИнфоКонтакта();
	
КонецПроцедуры
&НаКлиенте
Процедура УдалитьСообщение(Команда)
	
	Если Не ЗначениеЗаполнено(Параметр_chatId) Тогда	
		ПоказатьСообщениеПользователю("Поле ""ID чата"" не заполнено", "Параметр_chatId");
		Возврат;
	КонецЕсли;
	Если Не ЗначениеЗаполнено(Параметр_idMessage) Тогда	
		ПоказатьСообщениеПользователю("Поле ""ID сообщения"" не заполнено", "Параметр_idMessage");
		Возврат;
	КонецЕсли;
	
	КомандаУдалитьСообщение();
	
КонецПроцедуры
&НаКлиенте
Процедура ИзменитьСообщение(Команда)
	
	Если Не ЗначениеЗаполнено(Параметр_chatId) Тогда	
		ПоказатьСообщениеПользователю("Поле ""ID чата"" не заполнено", "Параметр_chatId");
		Возврат;
	КонецЕсли;
	Если Не ЗначениеЗаполнено(Параметр_idMessage) Тогда	
		ПоказатьСообщениеПользователю("Поле ""ID сообщения"" не заполнено", "Параметр_idMessage");
		Возврат;
	КонецЕсли;
	
	КомандаИзменитьСообщение();
	
КонецПроцедуры
&НаКлиенте
Процедура ПолучитьЧаты(Команда)
	
	КомандаПолучитьЧаты();
	
КонецПроцедуры
&НаКлиенте
Процедура ОтправитьУведомлениеОНабореТекста(Команда)
	
	Если Не ЗначениеЗаполнено(Параметр_chatId) Тогда	
		ПоказатьСообщениеПользователю("Поле ""ID чата"" не заполнено", "Параметр_chatId");
		Возврат;
	КонецЕсли;
	
	КомандаОтправитьУведомлениеОНабореТекста();
	
КонецПроцедуры

#КонецОбласти

#Область Партнеры

&НаКлиенте
Процедура ПолучитьВсеИнстансы(Команда)
	
	ОчиститьСообщения();
	
	Если Не ЗначениеЗаполнено(Объект.PartnerToken) Тогда
		Текст = "Поле ""Partner token"" не заполнено"; 		
		ПоказатьСообщениеПользователю(Текст, "Объект.PartnerToken");
		Возврат;
	КонецЕсли;
	
	Подключение = ПроверитьПодключение(); 
	
	Если Не Подключение Тогда	
		Возврат;
	КонецЕсли;
	
	ПолучитьВсеИнстансыТелеграм();
    
КонецПроцедуры
&НаКлиенте
Процедура СоздатьИнстанс(Команда)
	
	ОчиститьСообщения();
	
	Если Не ЗначениеЗаполнено(Объект.PartnerToken) Тогда			
		Текст = "Поле ""Partner token"" не заполнено"; 		
		ПоказатьСообщениеПользователю(Текст, "Объект.PartnerToken");
		Возврат;
	КонецЕсли;
	
	Подключение = ПроверитьПодключение(); 
	
	Если Не Подключение Тогда	
		Возврат;
	КонецЕсли;
	
	СоздатьИнстансТелеграм();
	
КонецПроцедуры
&НаКлиенте
Процедура УдалитьИнстанс(Команда)
	
	ОчиститьСообщения();
	
	Если Не ЗначениеЗаполнено(Параметр_idInstance) Тогда			
		Текст = "Поле ""ID инстанса"" не заполнено"; 		
		ПоказатьСообщениеПользователю(Текст, "ПараметрИдИнстанса");
		Возврат;
	КонецЕсли;
	
	Подключение = ПроверитьПодключение(); 
	
	Если Не Подключение Тогда	
		Возврат;
	КонецЕсли;	
	
	Оповещение = Новый ОписаниеОповещения("ПослеЗакрытияВопросаУдалитьИнстанс", ЭтотОбъект);	
	ПоказатьВопрос(Оповещение, "Удалить инстанс?", РежимДиалогаВопрос.ДаНет);
	
КонецПроцедуры
&НаКлиенте
Процедура ПослеЗакрытияВопросаУдалитьИнстанс(Результат, Параметры) Экспорт
	
    Если Результат = КодВозвратаДиалога.Нет Тогда
        Возврат;
    КонецЕсли;	
	
	УдалитьИнстансТелеграм();

КонецПроцедуры 

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область НастройкаФормы

&НаСервере
Процедура ПодготовитьНастройки()

	Параметр_message    = "Hello world!";
	СтатусСервиса             = "Неавторизован";
	Заголовок      			  = ОбработкаОбъект().ВерсияОбработки();
	ИнтервалОтправкиСообщений = 500;
	Интервал                  = 1440;
	
	Параметр_typingTime = 1000;

КонецПроцедуры


#КонецОбласти

#Область ОбработкиСостояний

&НаСервере
Процедура ОтобразитьОшибку(ТекстОшибки)
	
	ОписаниеОшибки = ТекстОшибки;
	Элементы.ГруппаОписаниеОшибки.Видимость = Истина;
	
КонецПроцедуры

&НаСервере
Процедура СброситьОшибки()
	
	ОписаниеОшибки = "";
	ПроизошлаОшибка = Ложь;
	Элементы.ГруппаОписаниеОшибки.Видимость = Ложь;
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьСтатусСервиса(НастройкиСохранены = Неопределено)	
	
	Если Не ПроверитьЗаполнение() Тогда
		Возврат;
	КонецЕсли;
	
	СброситьОшибки();
	Обработка = ОбработкаОбъект();
	СтатусСервиса = Обработка.ПолучитьСтатусСервиса();
	СервисАвторизован = СтатусСервиса = НРег("authorized");
	
	Если СервисАвторизован Тогда

		СтатусСервиса	 = СтатусАвторизован();
		НастройкиСервиса = Обработка.ПолучитьНастройкиАккаунта();
		
		Элементы.СтатусПроверки.ЦветТекста = WebЦвета.ТемноЗеленый;
		
		Если НастройкиСервиса <> Неопределено Тогда
            УстановитьНастройкиИзAPI(НастройкиСервиса);
            webhookUrl = НастройкиСервиса.webhookUrl;
            
            Если НастройкиСервиса.Свойство("delaySendMessagesMilliseconds") 
               И ЗначениеЗаполнено(НастройкиСервиса.delaySendMessagesMilliseconds) Тогда
                ИнтервалОтправкиСообщений = НастройкиСервиса.delaySendMessagesMilliseconds;
            КонецЕсли;
            
            Элементы.ДекорацияНастройкиНедоступны.Видимость = Ложь;
            Элементы.ДекорацияНастройкиНедоступныПрочее.Видимость = Ложь;
        КонецЕсли;
	Иначе
		
		Элементы.СтатусПроверки.ЦветТекста = WebЦвета.Красный;
		
		Если СтатусСервиса = "notAuthorized" Тогда			
			СтатусСервиса = "Неавторизован";		
		КонецЕсли;
		
		Элементы.ДекорацияНастройкиНедоступны.Видимость = Истина;		
		Элементы.ДекорацияНастройкиНедоступныПрочее.Видимость = Истина;
	КонецЕсли;
	
КонецПроцедуры


&НаСервере
Процедура УстановитьНастройкиИзAPI(НастройкиAPI)
	
	Реквизиты = ПолучитьРеквизиты();
	Для Каждого Реквизит Из Реквизиты Цикл
		Если СтрНачинаетсяС(Реквизит.Имя, "Параметр_") Тогда
			
			Имя = Сред(Реквизит.Имя, 10);
			Значение = Неопределено;
			Если НастройкиAPI.Свойство(Имя, Значение) Тогда
				ЭтотОбъект[Реквизит.Имя] = ?(ТипЗнч(ЭтотОбъект[Реквизит.Имя]) = Тип("Булево"), НРег(Значение) = "yes", Значение);
			КонецЕсли;
			
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры


&НаСервере
Функция ПроверитьСтандартныеНастройкиПолученияДанных(НастройкиСервиса, Знач НайденноеЗначение = Неопределено)

	НастройкиСервиса.Свойство("webhookUrl", НайденноеЗначение);

	Если Не НайденноеЗначение = "" Тогда
		Возврат Истина;
	КонецЕсли;

	НастройкиСервиса.Свойство("webhookUrlToken", НайденноеЗначение);

	Если Не НайденноеЗначение = "" Тогда
		Возврат Истина;
	КонецЕсли;

	НастройкиСервиса.Свойство("incomingWebhook", НайденноеЗначение);

	Если Не НайденноеЗначение = "yes" Тогда
		Возврат Истина;
	КонецЕсли;

	НастройкиСервиса.Свойство("outgoingWebhook", НайденноеЗначение);

	Если Не НайденноеЗначение = "yes" Тогда
		Возврат Истина;
	КонецЕсли;

	НастройкиСервиса.Свойство("stateWebhook", НайденноеЗначение);

	Если Не НайденноеЗначение = "yes" Тогда
		Возврат Истина;
	КонецЕсли;

	Возврат Ложь;

КонецФункции

&НаКлиенте
Функция ПроверитьПодключение(Команда = Неопределено)
	
	ОбновитьСтатусСервиса();
	СервисАвторизован =  СервисАвторизован();

	Если СервисАвторизован И Команда <> Неопределено Тогда
		ПоказатьПредупреждение(,"Авторизация выполнена успешно.");
	Иначе
		КомандаПолучитьQRКод();
	КонецЕсли;
	
	Возврат СервисАвторизован;
	
КонецФункции

&НаСервере
Функция СервисАвторизован()
	
	Возврат СтатусСервиса = СтатусАвторизован();
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция СтатусАвторизован()
	
	Возврат "Авторизован";
	
КонецФункции

#КонецОбласти

&НаСервере
Функция ОбработкаОбъект() 
	Возврат РеквизитФормыВЗначение("Объект");
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Процедура ПоказатьСообщениеПользователю(ТекстСообщения, Поле = "", Отказ = Истина)

	Сообщение       = Новый СообщениеПользователю;
	Сообщение.Текст = ТекстСообщения;
	Сообщение.Поле  = Поле;
	Сообщение.Сообщить();

	Отказ = Истина;

КонецПроцедуры

&НаКлиенте
Процедура ПараметрыХостовПоУмоланию(СброситьХост = Истина, СброситьХостМедиа = Истина)

	Если СброситьХост Тогда
		Объект.host = "api.green-api.com";
	КонецЕсли;

	Если СброситьХостМедиа Тогда
		Объект.mediaHost = "media.green-api.com";
	КонецЕсли;

КонецПроцедуры

&НаСервере
Функция ПолучитьИмяВнешнейОбработки(ДвоичныеДанные)
АдресВоВременномХранилище = ПоместитьВоВременноеХранилище(ДвоичныеДанные);
Возврат ВнешниеОбработки.Подключить(АдресВоВременномХранилище );
КонецФункции

&НаКлиенте
Функция ОткрытьФормуОбработки(ИмяФормы)
	
	ИмяОткрФормы = СтрШаблон("%1.%2", Лев(Этаформа.ИмяФормы, СтрНайти(Этаформа.ИмяФормы, ".", НаправлениеПоиска.СКонца) - 1), ИмяФормы);
	ОткрытьФорму(ИмяОткрФормы, Новый Структура("Ключ", Объект), ЭтотОбъект, , , , , РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);

КонецФункции

&НаСервере
Функция ПолучитьПараметрыиНастройки(ИмяНастройки)
	
	Строки = Объект.Настройки.НайтиСтроки(Новый Структура("Имя", ИмяНастройки));
	Если Строки.Количество() И Не ПустаяСтрока(Строки[0].ОбязательныеПараметры) Тогда
		
		ПараметрыЗапроса = Новый Структура;	
		ОбязательныеПараметры = Новый Структура(Строки[0].ОбязательныеПараметры);
		Для Каждого Параметр Из ОбязательныеПараметры Цикл
			
			Значение = ЭтотОбъект["Параметр_" + Параметр.Ключ];
			Если ЗначениеЗаполнено(Значение) Тогда
				
				ПараметрыЗапроса.Вставить(Параметр.Ключ, ?(ТипЗнч(Значение) = Тип("СписокЗначений"),
																			Значение.ВыгрузитьЗначения(),
																			Значение));
			КонецЕсли;
		КонецЦикла;
		
		Возврат ПараметрыЗапроса;
		
	КонецЕсли;
	
КонецФункции

&НаСервере
Функция ПолучитьОбязательныеПараметры(ИмяНастройки)
	
	Строки = ОбработкаОбъект().Настройки.НайтиСтроки(Новый Структура("Имя", ИмяНастройки));
	Если Строки.Количество() И Не ПустаяСтрока(Строки[0].ОбязательныеПараметры) Тогда
		Возврат Строки[0].ОбязательныеПараметры;
	Иначе
		Возврат "";
	КонецЕсли;
	
КонецФункции

&НаСервере
Функция ПолучитьПутьКДанным(ИмяЭлемента)
	
	Возврат Элементы[ИмяЭлемента].ПутьКДанным;
	
КонецФункции

Функция ИзвлечИзИмениПутьКAPI(ИмяЭлемента)
	
	Действия = СтрРазделить(ИмяЭлемента, "_");
	Возврат Новый Структура("Группа, Имя, Консоль",
					Действия[0],
					Действия[1],
					?(Действия.Количество() > 2,
						Сред(ТекущийЭлемент.Имя, СтрНайти(ТекущийЭлемент.Имя, "_",,,2) + 1),
						Неопределено));
	
КонецФункции

&НаСервере
Функция СтруктураИзJSON(JSON)
	
	Чтение = Новый ЧтениеJSON();
	Чтение.УстановитьСтроку(JSON);
	Конв = ПрочитатьJSON(Чтение);
	Чтение.Закрыть();
	Возврат Конв;
КонецФункции

&НаСервере
Функция СтруктураВJSONСтроку(Структура)
	Запись = Новый ЗаписьJSON();
	Запись.УстановитьСтроку();
	ЗаписатьJSON(Запись, Структура);
	Возврат Запись.Закрыть();
КонецФункции

&НаСервере
Процедура ВыбратьСсылкуДляОтправкиСервер(АдресФайла, ИмяФайла)
	
	Ответ = ОбработкаОбъект().ОтправитьВидеоАудиоИзображениеДокументПоURL(Параметр_chatId, АдресФайла, ИмяФайла, Параметр_message);	
	КонсольОтправка = СтруктураВJSONСтроку(Ответ);
	Сообщить("Файл отправлен успешно. idMessage=" + Ответ.idMessage);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьФотоДляАватара(ВыбранныеФайлы, ДопПараметры) Экспорт
	
	Если ВыбранныеФайлы = Неопределено Или ВыбранныеФайлы.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Файл = Новый Файл(ВыбранныеФайлы[0]);
	
	АдресХранилища = ПоместитьВоВременноеХранилище(Новый ДвоичныеДанные(Файл.ПолноеИмя));
	
	ВыбратьФотоДляАватараСервер(АдресХранилища, Файл.Имя);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьФайлДляОтправки(ВыбранныеФайлы, ДопПараметры) Экспорт
	
	Если ВыбранныеФайлы = Неопределено Или ВыбранныеФайлы.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Файл = Новый Файл(ВыбранныеФайлы[0]);
	
	АдресХранилища = ПоместитьВоВременноеХранилище(Новый ДвоичныеДанные(Файл.ПолноеИмя));
	
	ВыбратьФайлДляОтправкиСервер(АдресХранилища, Файл.Имя);
	
КонецПроцедуры

&НаСервере
Процедура ВыбратьФайлДляОтправкиСервер(АдресХранилища, ИмяФайла)
	
	ИмяВременногоФайла = КаталогВременныхФайлов() + ИмяФайла;
	
	ДвоичныеДанные = ПолучитьИзВременногоХранилища(АдресХранилища);
	ДвоичныеДанные.Записать(ИмяВременногоФайла);
	
	// --- НАЧАЛО ИЗМЕНЕНИЙ ---
	Попытка
		// Здесь мы передаем Новый Файл(), как исправляли ранее
		Ответ = ОбработкаОбъект().ОтправитьВидеоАудиоИзображениеДокумент(Параметр_chatId, ИмяВременногоФайла, Параметр_message);
		
		КонсольОтправка = СтруктураВJSONСтроку(Ответ);
		Сообщить("Файл отправлен успешно. idMessage=" + Ответ.idMessage);
		
	Исключение
		// Выводим полный текст ошибки пользователю
		Описание = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		Сообщить("НЕ УДАЛОСЬ ОТПРАВИТЬ ФАЙЛ:" + Символы.ПС + Описание, СтатусСообщения.Важное);
		
		// Дополнительно пишем в журнал регистрации для админа
		ЗаписьЖурналаРегистрации("GreenAPI.Форма", УровеньЖурналаРегистрации.Ошибка,,, Описание);
	КонецПопытки;
	// --- КОНЕЦ ИЗМЕНЕНИЙ ---
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьФайлДляЗагрузки(ВыбранныеФайлы, ДопПараметры) Экспорт
	
	Если ВыбранныеФайлы = Неопределено Или ВыбранныеФайлы.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Файл = Новый Файл(ВыбранныеФайлы[0]);
	
	АдресХранилища = ПоместитьВоВременноеХранилище(Новый ДвоичныеДанные(Файл.ПолноеИмя));
	
	ОтправитьФайл = ДопПараметры = Истина;
	
	ВыбратьФайлДляЗагрузкиСервер(АдресХранилища, Файл.Имя, ОтправитьФайл);
	
КонецПроцедуры

&НаСервере
Процедура ВыбратьФайлДляЗагрузкиСервер(АдресХранилища, ИмяФайла, ОтправитьФайл = Ложь)
	
	ИмяВременногоФайла = КаталогВременныхФайлов() + ИмяФайла;
	
	ДвоичныеДанные = ПолучитьИзВременногоХранилища(АдресХранилища);
	ДвоичныеДанные.Записать(ИмяВременногоФайла);
	
	Ответ = ОбработкаОбъект().ЗагрузитьФайл(ИмяВременногоФайла);
	
	Если ОтправитьФайл Тогда
		ВыбратьСсылкуДляОтправкиСервер(Ответ.urlFile, ИмяФайла);
	Иначе
		КонсольОтправка = СтруктураВJSONСтроку(Ответ);
		Сообщить("Файл отправлен успешно. urlFile=" + Ответ.urlFile);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПолучитьВсеСообщенияЧерезВебхукСервер()
	
	ОбработкаОбъект = ОбработкаОбъект();
	
	Пока Истина Цикл 
		
		Ответ = ОбработкаОбъект.ПолучитьСообщение(); 
		
		Если Не ЗначениеЗаполнено(Ответ) Тогда		
			Прервать;
		КонецЕсли;		
		
	КонецЦикла;
	
	ЗначениеВРеквизитФормы(ОбработкаОбъект, "Объект"); 	
	Объект.ТекстПолученноеСообщение = СтруктураВJSONСтроку(Ответ);
	
КонецПроцедуры

#КонецОбласти

